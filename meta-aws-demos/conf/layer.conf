# We have a conf and classes directory, add to BBPATH
BBPATH .= ":${LAYERDIR}"

# We have recipes-* directories, add to BBFILES
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb \
            ${LAYERDIR}/recipes-*/*/*.bbappend"

# this is necessary to find aws-demo-image
BBFILES += "${LAYERDIR}/recipes-*/*/*/*.bb \
            ${LAYERDIR}/recipes-*/*/*/*.bbappend"

BBFILE_COLLECTIONS += "aws-demos"
BBFILE_PATTERN_aws-demos := "^${LAYERDIR}/"
BBFILE_PRIORITY_aws-demos = "99"

# This should only be incremented on significant changes that will
# cause compatibility issues with other layers
LAYERVERSION_aws-demos = "1"
LAYERSERIES_COMPAT_aws-demos = "kirkstone"
LAYERDEPENDS_aws-demos = "core meta-aws"

# The dynamic-layers directory hosts the extensions and layer specific
# modifications.
#
# The .bbappend and .bb files are included if the respective layer
# collection is available.
BBFILES += "${@' '.join('${LAYERDIR}/dynamic-layers/%s/recipes*/*/*.bbappend' % layer \
               for layer in BBFILE_COLLECTIONS.split())}"
BBFILES += "${@' '.join('${LAYERDIR}/dynamic-layers/%s/recipes*/*/*.bb' % layer \
               for layer in BBFILE_COLLECTIONS.split())}"

# this is necessary to allow overwrites of dynamic-layers in dynamic-layers
BBFILES += "${@' '.join('${LAYERDIR}/dynamic-layers/%s/dynamic-layers/*/recipes*/*/*.bbappend' % layer \
               for layer in BBFILE_COLLECTIONS.split())}"
BBFILES += "${@' '.join('${LAYERDIR}/dynamic-layers/%s/dynamic-layers/*/recipes*/*/*.bb' % layer \
               for layer in BBFILE_COLLECTIONS.split())}"

BBFILES_DYNAMIC += " \
    alb:${LAYERDIR}/dynamic-layers/alb/*/*/*.bb \
    alb:${LAYERDIR}/dynamic-layers/alb/*/*/*.bbappend \
"
